/*
 *  Build file for gradle
 */


plugins {
    id 'com.palantir.git-version' version '3.0.0'
    id 'maven-publish'
	id 'signing'
}


apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'


task sourcesJar(type: Jar) {
	 archiveClassifier = 'sources'
	 manifest {
        attributes("Built-By": "Mathieu Fortin and Jean-Francois Lavoie",
        		   "Specification-Title": "REpicea Simulation sources",
	       		   "Specification-Version": project.version,                   
                   "Specification-Vendor": "CWFC-CCFB",
                   "Implementation-Title": "repicea-simulation",
                   "Implementation-Version": project.version,
                   "Implementation-Vendor": "CWFC-CCFB",
                   )
    }
   	duplicatesStrategy = DuplicatesStrategy.EXCLUDE		// to avoid duplicate files
    
	into ('/') {
		from files(['./README.md', './LICENSE.md'])			
	}
	
	// include all java files from main sourceset into the jar file (resources are already included)
	from sourceSets.main.java
	from sourceSets.main.resources		
}

java {
  toolchain { 
        languageVersion = JavaLanguageVersion.of(8)
  }
  withSourcesJar()
  withJavadocJar()
}

repositories {
    mavenCentral()
    mavenLocal()
}

println "Using java version " + JavaVersion.current()

sourceSets {
    main {    	
        java {
            srcDirs 'src'            
        }
        
        resources {
			srcDirs= ['src']
			exclude '**/*.java'
		}
		
		output.resourcesDir = "$buildDir/classes/java/$name"
    }

    test {
        java {
            srcDirs 'test'
        }
                
        resources {
			srcDirs= ['test']
			exclude '**/*.java'
		}
		
		output.resourcesDir = "$buildDir/classes/java/$name"
    }               
}

configurations {
	repiceaImplementation {
		extendsFrom implementation
		description = 'Main implementation'
		canBeResolved = true
	}
}

// make sure to set the compliance to Java 1.8 in the project properties if the Java version is later than 1.8
// make sure to set Java Compiler > Errors Warnings > Deprecated and Restricted API > Forbidden reference to WARNING instead of ERROR
dependencies {
    implementation group: 'io.github.cwfc-ccfb', name: 'repicea', version: '[1.15.4,)'    
    implementation group: 'io.github.cwfc-ccfb', name: 'repicea-mathstats', version: '[1.7.6,)'    

    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '[2.14.2,)'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '[2.14.2,)'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '[2.14.2,)'

    testImplementation "junit:junit:4.13.2"    
}

compileJava {
	project.version= gitVersion() 
	println "Building " + rootProject.name + " " + project.version
	println "Installation directory " + "../externallibraries/" + rootProject.name
}

test {
    // show standard out and standard error of the test JVM(s) on the console
    testLogging.showStandardStreams = true

    // set heap size for the test JVM(s)
    maxHeapSize = "2048m"
    
    exclude '**/ProductionEnvironmentTest.class'
}

jar {	
	 manifest {
        attributes("Built-By": "Mathieu Fortin and Jean-Francois Lavoie",
        		   "Specification-Title": "REpicea Simulation",
	       		   "Specification-Version": project.version,                   
                   "Specification-Vendor": "CWFC-CCFB",
                   "Implementation-Title": "repicea-simulation",
                   "Implementation-Version": project.version,
                   "Implementation-Vendor": "CWFC-CCFB",
                   )
    }
    
	into ('/') {
		from files(['./README.md', './LICENSE.md'])			
	}
	
	// include all java files from main sourceset into the jar file (resources are already included)
	from sourceSets.main.allJava		
}

task integrationTest(type: Test) {
	description = 'Runs the integration tests (unit tests run using the produced JAR file)'
    group = 'verification'
	dependsOn jar
	
	include '**/*.class'
	
	testLogging.showStandardStreams = true
    testClassesDirs = sourceSets.test.output.classesDirs
        
    // set classpath to test classes + test dependencies + JAR
    classpath = project.sourceSets.test.output + configurations.testRuntimeClasspath + files(jar.archiveFile) 
    outputs.upToDateWhen { false }
    mustRunAfter test
    maxHeapSize = "2048m"
}

check.dependsOn integrationTest

task deleteInstallFolder(type: Delete) {
  delete "../externallibraries/" + rootProject.name
}

task install(type: Copy) {
	description = 'Copies the output jar to installation folder.'
	group = 'build'
	dependsOn deleteInstallFolder,integrationTest
	from configurations.repiceaImplementation
	from jar
	into "../externallibraries/" + rootProject.name
}

publishing {
    publications {
        repicea(MavenPublication) {
          	from components.java
        	pom {
        		name = 'REpicea Simulation'
				group = 'io.github.cwfc-ccfb'
        		description = 'Simulation methods'
        		url = 'https://github.com/CWFC-CCFB/repicea-simulation'
        		properties = [
        			packaging: "jar"
	       		]
        		licenses {
        			license {
        				name = 'GNU Lesser General Public License 3.0'
        				url  = 'https://www.gnu.org/licenses/lgpl-3.0'
        			}
        		}
        		developers {
        			developer {
        				id = 'matfortin'
        				name = 'Mathieu Fortin'
        				email = 'mathieu.fortin.re@gmail.com'
        			}        
        			developer {
        				id = 'jflavoie'
        				name = 'Jean-Francois Lavoie'
        				email = 'atmailjfl@gmail.com'
        			}
        		}
        		
                scm {
          			connection = 'scm:git:https://github.com/CWFC-CCFB/repicea-simulation'
          			developerConnection = 'scm:git:https://github.com/CWFC-CCFB/repicea-simulation'
          			url = 'https://github.com/CWFC-CCFB/repicea-simulation'
        		}
		     }	
        }
    }
    repositories {
        maven {
            name = 'LocalFile'
            url = "file://${buildDir}/repo"
        }
    }
}

signing {
    sign publishing.publications.repicea
}

publishRepiceaPublicationToMavenLocal {
	dependsOn integrationTest
}

publishRepiceaPublicationToLocalFileRepository {
	dependsOn clean, integrationTest
}
